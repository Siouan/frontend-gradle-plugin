
plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'jacoco'
    id 'com.gradle.plugin-publish'
}

repositories {
    jcenter()
    mavenCentral()
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}

tasks.named('test', Test) { t ->
    t.useJUnitPlatform()
    gradle.addListener(new SiouanTestListener(logger))
}

tasks.named('jacocoTestReport') {
    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-compress:1.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

group 'org.siouan'
version '2.2.0'
description 'Build Node.js, npm, Yarn applications with Gradle.'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

gradlePlugin {
    plugins {
        frontend {
            id = 'org.siouan.frontend'
            implementationClass = 'org.siouan.frontendgradleplugin.FrontendGradlePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/siouan/frontend-gradle-plugin'
    vcsUrl = 'https://github.com/siouan/frontend-gradle-plugin.git'

    plugins {
        frontend {
            displayName = 'Frontend Gradle plugin'
            description = 'Build Node.js, npm, Yarn applications with Gradle.'
            tags = ['node', 'nodejs', 'npm', 'npx', 'yarn', 'frontend']
        }
    }
}

class SiouanTestListener implements TestListener, TestOutputListener {
    Logger logger;

    SiouanTestListener(Logger logger) {
        this.logger = logger;
    }

    void beforeSuite(TestDescriptor testSuite) {}

    void afterSuite(TestDescriptor testSuite, TestResult result) {}

    void beforeTest(TestDescriptor testDescriptor) {
        logger.lifecycle('\n========== TEST {}.{} STARTED ==========',
                testDescriptor.className.substring(testDescriptor.className.lastIndexOf('.') + 1),
                testDescriptor.displayName);
    }

    void afterTest(TestDescriptor testDescriptor, TestResult result) {
        logger.lifecycle('========== TEST {}.{} {} ==========',
                testDescriptor.className.substring(testDescriptor.className.lastIndexOf('.') + 1),
                testDescriptor.displayName, result.resultType);
    }

    void onOutput(TestDescriptor testDescriptor, TestOutputEvent outputEvent) {
        if (!outputEvent.message.trim().isBlank()) {
            logger.lifecycle('  {}', outputEvent.message);
        }
    }
}
